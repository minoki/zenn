---
title: "unsafePerformIOã§ã¯ã˜ã‚ã‚‹æ„‰å¿«ãªHaskellãƒ—ãƒ­ã‚°ãƒ©ãƒŸãƒ³ã‚°"
emoji: "ğŸ˜½"
type: "tech" # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: ["haskell"]
published: true
---

Haskellã¯ã€Œå…¥å‡ºåŠ›ã®ãŸã‚ã«IOãƒ¢ãƒŠãƒ‰ã‚’ä½¿ã†å¿…è¦ãŒã‚ã‚‹ã®ã§é›£ã—ã„ã€ã¨ã„ã†ã‚¤ãƒ¡ãƒ¼ã‚¸ã‚’æŒãŸã‚ŒãŒã¡ã§ã™&lbrack;è¦å‡ºå…¸&rbrack;ã€‚ã“ã®è¨˜äº‹ã§ã¯ãã®ã‚¤ãƒ¡ãƒ¼ã‚¸ã«ç•°ã‚’å”±ãˆã‚‹ã“ã¨ã‚’ç›®æŒ‡ã—ã¾ã™ã€‚ã™ãªã‚ã¡ã€ãƒ¢ãƒŠãƒ‰ã‚’ä½¿ã‚ãšã«å…¥å‡ºåŠ›ã‚’å«ã‚€ãƒ—ãƒ­ã‚°ãƒ©ãƒŸãƒ³ã‚°ã‚’ã‚„ã£ã¦ã¿ã¾ã™ã€‚

ã¨è¨€ã£ã¦ã‚‚ã€Haskellæ¨™æº–ã®å…¥å‡ºåŠ›é–¢æ•°ã«ã¯ `IO` å‹ãŒã¤ã„ã¦ã„ã‚‹ã“ã¨ã«å¤‰ã‚ã‚Šã¯ã‚ã‚Šã¾ã›ã‚“ã€‚ã§ã™ãŒã€ `unsafePerformIO` ã¨ã„ã†é­”æ³•ã®é–¢æ•°ã‚’ä½¿ã†ã¨ã€`IO` å‹ã‚’å¤–ã—ã¦å€¤ã‚’å–ã‚Šå‡ºã™ã“ã¨ãŒã§ãã‚‹ã®ã§ã™ã€‚ã“ã®è¨˜äº‹ã§ã¯ã“ã‚Œã‚’æœ€å¤§é™æ´»ç”¨ã—ã¾ã™ã€‚

```haskell
module System.IO.Unsafe where

-- é­”æ³•ã®é–¢æ•°
unsafePerformIO :: IO a -> a
```

`IO` ã‚’æ°—ã«ã—ãªãã¦è‰¯ã„æ¥½åœ’ã¸ã€ã„ã–ï¼

## æ–‡å­—åˆ—ã®å‡ºåŠ›

ä¾‹ãˆã°ã€æ–‡å­—åˆ—ã‚’å‡ºåŠ›ã—ã¦æ”¹è¡Œã™ã‚‹ `putStrLn` é–¢æ•°ã®ã€ŒIOãŒã¤ã‹ãªã„ã€ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã¯æ¬¡ã®ã‚ˆã†ã«æ›¸ã‘ã¾ã™ï¼š

```haskell
{-# OPAQUE putStrLn' #-}
putStrLn' :: String -> ()
putStrLn' x = unsafePerformIO (putStrLn x)
```

`OPAQUE` ãƒ—ãƒ©ã‚°ãƒã¯ãŠã¾ã˜ãªã„ã ã¨æ€ã£ã¦ãã ã•ã„ã€‚ã“ã‚Œã¯GHC 9.4ã§è¿½åŠ ã•ã‚ŒãŸæ©Ÿèƒ½ãªã®ã§ã€ã“ã®è¨˜äº‹ã®ã‚³ãƒ¼ãƒ‰ã‚’è©¦ã™éš›ã¯GHC 9.4ä»¥é™ã§å®Ÿè¡Œã—ã¦ãã ã•ã„ã€‚

`putStrLn'` ã‚’ä½¿ã†ã¨ã€Hello worldã¯æ¬¡ã®ã‚ˆã†ã«æ›¸ã‘ã¾ã™ï¼š

```haskell
-- Main1.hs
import System.IO.Unsafe (unsafePerformIO)

{-# OPAQUE putStrLn' #-}
putStrLn' :: String -> ()
putStrLn' x = unsafePerformIO (putStrLn x)

main' :: ()
main' = putStrLn' "Hello world!"

main = main' `seq` pure ()
```

`main'` ãŒæˆ‘ã€…ã®æ›¸ããŸã„ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã§ã€`main` ã¯ãã‚Œã‚’å®Ÿè¡Œã™ã‚‹ãŸã‚ã®ãŠã¾ã˜ãªã„ã§ã™ã€‚

å®Ÿè¡Œçµæœã¯ã“ã†ãªã‚Šã¾ã™ï¼š

```
$ ghc Main1.hs
$ ./Main1
Hello world!
```

ã„ã„æ„Ÿã˜ã§ã™ã­ã€‚Haskellå®Œå…¨ã«ç†è§£ã—ãŸã€‚

## æœ€é©åŒ–ã¨æˆ¦ã†

ä»Šåº¦ã¯Hello worldã‚’2å›è¡¨ç¤ºã—ã¦ã¿ã¾ã—ã‚‡ã†ã€‚

```haskell
-- Main2.hs
import System.IO.Unsafe (unsafePerformIO)

{-# OPAQUE putStrLn' #-}
putStrLn' :: String -> ()
putStrLn' x = unsafePerformIO (putStrLn x)

main' :: ()
main' = case putStrLn' "Hello world!" of
          () -> putStrLn' "Hello world!"

main = main' `seq` pure ()
```

ã“ã‚Œã‚‚å®Ÿè¡Œã—ã¦ã¿ã¾ã—ã‚‡ã†ï¼š

```
$ ghc Main2.hs
$ ./Main2
Hello world!
Hello world!
```

ã„ã„æ„Ÿã˜ã§ã™ã­ã€‚â€¦â€¦æœ¬å½“ã«ï¼Ÿ

å¿µã®ç‚ºã€æœ€é©åŒ–ã‚’æœ‰åŠ¹ã«ã—ã¦ã‚³ãƒ³ãƒ‘ã‚¤ãƒ«ãƒ»å®Ÿè¡Œã—ã¦ã¿ã¾ã—ã‚‡ã†ï¼š

```
$ ghc -O2 Main2.hs
$ ./Main2
Hello world!
```

ãªã‚“ã¦ã“ã£ãŸï¼1å›ã—ã‹è¡¨ç¤ºã•ã‚Œãªã„ï¼

ã“ã‚Œã¯ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ãƒ¼ãŒã€**å…±é€šéƒ¨åˆ†å¼ã®å‰Šé™¤** (common subexpression elimination) ã¨ã„ã†æœ€é©åŒ–ã‚’è¡Œãªã£ãŸã“ã¨ã«ã‚ˆã‚Šã¾ã™ã€‚ã¤ã¾ã‚Šã€ã€ŒåŒã˜é–¢æ•°ã‚’åŒã˜å¼•æ•°ã§å‘¼ã³å‡ºã—ãŸã‚‰çµæœã¯åŒã˜ã ã‚ˆã­â˜†ã€ã¨ã„ã†ã“ã¨ã§ã€`main'` é–¢æ•°ã®ä¸­èº«ãŒæ¬¡ã®ã‚ˆã†ã«æœ€é©åŒ–ã•ã‚ŒãŸã®ã§ã™ï¼š

```haskell
main' :: ()
main' = let a = putStrLn' "Hello world!"
        in case a of
             () -> a
```

å…±é€šéƒ¨åˆ†å¼ã®å‰Šé™¤ã¯ã€ã©ã‚“ãªãƒ—ãƒ­ã‚°ãƒ©ãƒŸãƒ³ã‚°è¨€èªã§ã‚‚ã§ãã‚‹ã‚ã‘ã§ã¯ã‚ã‚Šã¾ã›ã‚“ã€‚ä¾‹ãˆã°ã€Cè¨€èªã®ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ãƒ¼ã¯ä¸­èº«ãŒã‚ã‹ã‚‰ãªã„é–¢æ•°ã®å‘¼ã³å‡ºã—ã‚’å‰Šæ¸›ã™ã‚‹ã“ã¨ã¯ã—ãªã„ã§ã—ã‚‡ã†ã€‚Haskellã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ãƒ¼ãŒã“ã‚Œã‚’è¡Œã†ã®ã¯ã€Haskellã®å…¨ã¦ã®é–¢æ•°ãŒ**ç´”ç²‹**ã ã¨æ€ã£ã¦ã„ã‚‹ã‹ã‚‰ã§ã™ã€‚

å¯¾ç­–ã¨ã—ã¦ã€é–¢æ•°ã«ãƒ€ãƒŸãƒ¼ã®å¼•æ•°ã‚’æŒãŸã›ã‚‹ã“ã¨ã«ã—ã¾ã™ã€‚å‹ã¯ `Dummy` ã¨ã—ã¦ã€ä¸­èº«ã¯ `Int` ã¨ã—ã¾ã™ã€‚

```haskell
-- Main2b.hs
import System.IO.Unsafe (unsafePerformIO)

newtype Dummy = Dummy Int

{-# OPAQUE putStrLn' #-}
putStrLn' :: String -> Dummy -> ()
putStrLn' x _ = unsafePerformIO (putStrLn x)

main' :: ()
main' = case putStrLn' "Hello world!" (Dummy 0) of
          () -> putStrLn' "Hello world!" (Dummy 1)

main = main' `seq` pure ()
```

å®Ÿè¡Œçµæœã¯æ¬¡ã®ã‚ˆã†ã«ãªã‚Šã¾ã™ï¼š

```
$ ghc -O2 Main2b.hs
$ ./Main2b
Hello world!
Hello world!
```

ä»Šåº¦ã“ãã€ã„ã„æ„Ÿã˜ã§ã™ã­ã€‚

ã‚‚ã¡ã‚ã‚“ã€é–¢æ•°ã‚’ã™ã¹ã¦ã‚¤ãƒ³ãƒ©ã‚¤ãƒ³åŒ–ã—ã¦ã—ã¾ãˆã°ãƒ€ãƒŸãƒ¼ã®å¼•æ•°ãŒå®Ÿéš›ã«ã¯åˆ©ç”¨ã•ã‚Œã¦ã„ãªã„ã“ã¨ãŒã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ãƒ¼ã«ãƒãƒ¬ã¦ã—ã¾ã„ã¾ã™ãŒã€`OPAQUE` ãƒ—ãƒ©ã‚°ãƒãŒã‚ã‚‹ã®ã§ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ãƒ¼ã¯ãã®ã“ã¨ãŒã‚ã‹ã‚Šã¾ã›ã‚“ã€‚

## é€æ¬¡å®Ÿè¡Œ

ä»Šåº¦ã¯ã‚‚ã†å°‘ã—å¯¾è©±çš„ãªãƒ—ãƒ­ã‚°ãƒ©ãƒ ã‚’æ›¸ã„ã¦ã¿ã¾ã—ã‚‡ã†ã€‚åå‰ã¨å¹´é½¢ã‚’å°‹ã­ã¦ã€ãã‚Œã‚’å‡ºåŠ›ã™ã‚‹æ„Ÿã˜ã§è¡Œãã¾ã—ã‚‡ã†ã€‚

æ–‡å­—åˆ—ã‚’æ¨™æº–å…¥åŠ›ã‹ã‚‰èª­ã¿å–ã‚‹ã«ã¯ `getLine` é–¢æ•°ãŒã€æ–‡å­—åˆ—ã‚’æ•´æ•°ã«å¤‰æ›ã™ã‚‹ã«ã¯ `read` é–¢æ•°ãŒä½¿ãˆã¾ã™ã€‚

```haskell
-- Main3.hs
import System.IO.Unsafe (unsafePerformIO)

newtype Dummy = Dummy Int

{-# OPAQUE putStrLn' #-}
putStrLn' :: String -> Dummy -> ()
putStrLn' x _ = unsafePerformIO (putStrLn x)

{-# OPAQUE getLine' #-}
getLine' :: Dummy -> String
getLine' dummy = unsafePerformIO (dummy `seq` getLine)

getInt :: Dummy -> Int
getInt dummy = read (getLine' dummy)

main' :: ()
main' = case putStrLn' "What is your name?" (Dummy 0) of
          () -> case getLine' (Dummy 1) of
                  name -> case putStrLn' "How old are you?" (Dummy 2) of
                            () -> case getInt (Dummy 3) of
                                    age -> putStrLn' (name ++ ", " ++ show age) (Dummy 4)

main = main' `seq` pure ()
```

å®Ÿè¡Œä¾‹ï¼š

```
$ ghc -O2 Main3.hs
$ ./Main3
What is your name?
How old are you?
mod_poppoâ†©ï¸
65535â†©ï¸
mod_poppo, 65535
```

ãŠã‚„ãŠã‚„ã€åå‰ã‚’èã„ã¦ã‹ã‚‰å¹´é½¢ã‚’å°‹ã­ãŸã„ã®ã«ã€å…ˆã«ã€ŒHow old are you?ã€ã¾ã§è¡¨ç¤ºã•ã‚Œã¦ã—ã¾ã„ã¾ã—ãŸã€‚ã©ã†ã‚„ã‚‰ã€é€æ¬¡å®Ÿè¡ŒãŒã§ãã¦ã„ãªã„ã‚ˆã†ã§ã™ã€‚

æ™®é€šã®ãƒ—ãƒ­ã‚°ãƒ©ãƒŸãƒ³ã‚°è¨€èªã§ã¯é–¢æ•°ã¯è¨˜è¿°ã—ãŸé †ç•ªã«è©•ä¾¡ã•ã‚Œã‚‹å¿…è¦ãŒã‚ã‚‹ã‹ã¨æ€ã„ã¾ã™ãŒã€Haskellã¯**ç´”ç²‹**ãªè¨€èªãªã®ã§ã€å¿…ãšã—ã‚‚ã‚½ãƒ¼ã‚¹ã‚³ãƒ¼ãƒ‰ã«æ›¸ã‹ã‚ŒãŸé †ç•ªã«è©•ä¾¡ã•ã‚Œã‚‹ã¨ã¯é™ã‚‰ãªã„ã®ã§ã™ã­ã€‚

ã¨ã‚‚ã‹ãã€é€æ¬¡å®Ÿè¡ŒãŒã§ããªã„ã®ã§ã¯ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã¨ã—ã¦æˆã‚Šç«‹ã¡ã¾ã›ã‚“ã€‚`seq` ã¨ã‹ `BangPatterns` ã‚’ä½¿ã£ãŸæŠ€ã‚‚ã‚ã‚Šã¾ã™ãŒã€ã“ã“ã¯æ­£æ”»æ³•ï¼ˆï¼Ÿï¼‰ã§è¡ŒããŸã„ã§ã™ã€‚

ã“ã“ã§ã¯ã€ãƒ€ãƒŸãƒ¼ã®å¤‰æ•°ã§ä¾å­˜é–¢ä¿‚ã‚’è¡¨ç¾ã™ã‚‹ã“ã¨ã«ã—ã¾ã™ã€‚ã¤ã¾ã‚Šã€é–¢æ•°ã‹ã‚‰ã¯æœ¬æ¥è¿”ã—ãŸã„å€¤ã«åŠ ãˆã¦ã€ãƒ€ãƒŸãƒ¼ã®å€¤ã‚’è¿”ã—ã€ãã‚Œã‚’æ¬¡ã«å®Ÿè¡Œã™ã‚‹é–¢æ•°ã«æ¸¡ã™ã®ã§ã™ã€‚

æ“¬ä¼¼ã‚³ãƒ¼ãƒ‰ã§èª¬æ˜ã™ã‚‹ã¨ã€

```haskell
let (dummy', x) = f dummy
    (dummy'', y) = g dummy'
    (dummy''', z) = h dummy''
```

ã¨ã„ã†é¢¨ã«ãƒ€ãƒŸãƒ¼ã®å¤‰æ•°ã‚’å—ã‘æ¸¡ã—ã™ã‚Œã°ã€ã€Œ`g` ã¯ `f` ã®è¿”ã‚Šå€¤ã«ä¾å­˜ã™ã‚‹ã®ã§ `g` ã‚ˆã‚Šã‚‚å‰ã« `f` ã‚’è©•ä¾¡ã—ãªã‘ã‚Œã°ãªã‚‰ãªã„ã€ã€Œ`h` ã¯ `g` ã®è¿”ã‚Šå€¤ã«ä¾å­˜ã™ã‚‹ã®ã§ `h` ã‚ˆã‚Šã‚‚å‰ã« `g` ã‚’è©•ä¾¡ã—ãªã‘ã‚Œã°ãªã‚‰ãªã„ã€ã¨ã„ã†ã®ãŒã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ãƒ¼ã«ä¼ã‚ã‚Šã¾ã™ã€‚

ï¼ˆHaskellã¯é…å»¶è©•ä¾¡ã§ã€`let` ã®æŸç¸›ã‚‚é…å»¶ã—ã¦èµ·ã“ã‚‹ã®ã§ã€ä¸Šè¨˜ã®ã‚³ãƒ¼ãƒ‰ãã®ã¾ã¾ã§ã¯ã€Œ`g` ã‚ˆã‚Šã‚‚å‰ã« `f` ãŒè©•ä¾¡ã•ã‚Œã‚‹ã€ã“ã¨ã¯ä¿è¨¼ã•ã‚Œã¾ã›ã‚“ã€‚ãªã®ã§ã€Œæ“¬ä¼¼ã‚³ãƒ¼ãƒ‰ã€ã§ã™ã€‚ï¼‰

ã“ã‚Œã¾ã§ã¯ãƒ€ãƒŸãƒ¼ã®å€¤ã¯1ãšã¤å¢—ã‚„ã—ã¦ãã¾ã—ãŸãŒã€`OPAQUE` ã§ãƒãƒ¼ã‚¯ã—ãŸé–¢æ•°ã®è¿”ã‚Šå€¤ã¯ã©ã£ã¡ã¿ã¡ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ãƒ¼çš„ã«ã¯æœªçŸ¥ã®å€¤ãªã®ã§ã€é–¢æ•°ã‹ã‚‰ã¯ãƒ€ãƒŸãƒ¼ã®å€¤ã¯ãã®ã¾ã¾è¿”ã—ã¦ã‚‚å¤§ä¸ˆå¤«ã§ã™ã€‚

ã“ã®æ”¹è‰¯ã‚’æ–½ã—ãŸã‚³ãƒ¼ãƒ‰ã¯æ¬¡ã®ã‚ˆã†ã«ãªã‚Šã¾ã™ï¼š

```haskell
-- Main3b.hs
import System.IO.Unsafe (unsafePerformIO)

newtype Dummy = Dummy Int

{-# OPAQUE putStrLn' #-}
putStrLn' :: String -> Dummy -> Dummy
putStrLn' x dummy = dummy `seq` unsafePerformIO (putStrLn x >> pure dummy)

{-# OPAQUE getLine' #-}
getLine' :: Dummy -> (Dummy, String)
getLine' dummy = dummy `seq` unsafePerformIO (getLine >>= \s -> pure (dummy, s))

getInt :: Dummy -> (Dummy, Int)
getInt dummy = let (dummy', s) = getLine' dummy
               in (dummy', read s)

main' :: Dummy
main' = let dummy0 = Dummy 0
            dummy1 = putStrLn' "What is your name?" dummy0
            (dummy2, name) = getLine' dummy1
            dummy3 = putStrLn' "How old are you?" dummy2
            (dummy4, age) = getInt dummy3
        in putStrLn' (name ++ ", " ++ show age) dummy4

main = main' `seq` pure ()
```

ï¼ˆ`putStrLn'` ã¨ `getLine'` ã®å†…éƒ¨ã§ `seq` ã‚’ä½¿ã£ã¦ã„ã‚‹ã˜ã‚ƒã‚“ã€ã¨ã„ã†ã®ã¯ç›®ã‚’ç‘ã£ã¦ãã ã•ã„ã€‚ãã“ã¯ãŠã¾ã˜ãªã„ãªã®ã§â€¦â€¦ã€‚ï¼‰

å®Ÿè¡Œä¾‹ï¼š

```
$ ghc -O2 Main3b.hs
$ ./Main3b
What is your name?
mod_poppoâ†©ï¸
How old are you?
65535â†©ï¸
mod_poppo, 65535
```

è‰¯ã„ã§ã™ã­ã€‚

## å‹ã‚’æ•´ãˆã‚‹

`Main3b.hs` ã®ã‚³ãƒ¼ãƒ‰ã‚’ã€æ„å‘³ã‚’å¤‰ãˆãªã„ç¯„å›²ã§å‹ã‚’æ•´ãˆã¦ã¿ã¾ã—ã‚‡ã†ã€‚ã¾ãšã€ `putStrLn'` ã®è¿”ã‚Šå€¤ã®å‹ã‚’ `(Dummy, ())` ã«å¤‰ãˆã‚‹ã¨ `getLine'` ã‚„ `getInt` ã¨ã®çµ±ä¸€æ„ŸãŒå‡ºã¾ã™ã€‚ã¾ãŸã€`main'` ã‚‚ `dummy0` ã‚’å¼•æ•°ã«å–ã‚‹ã‚ˆã†ã«ã—ã¾ã™ã€‚æ•´ãˆãŸå¾Œã®ã‚³ãƒ¼ãƒ‰ã¯æ¬¡ã®ã‚ˆã†ã«ãªã‚Šã¾ã™ï¼š

```haskell
-- Main3c.hs
import System.IO.Unsafe (unsafePerformIO)

newtype Dummy = Dummy Int

{-# OPAQUE putStrLn' #-}
putStrLn' :: String -> Dummy -> (Dummy, ())
putStrLn' x dummy = dummy `seq` unsafePerformIO (putStrLn x >> pure (dummy, ()))

{-# OPAQUE getLine' #-}
getLine' :: Dummy -> (Dummy, String)
getLine' dummy = dummy `seq` unsafePerformIO (getLine >>= \s -> pure (dummy, s))

getInt :: Dummy -> (Dummy, Int)
getInt dummy = let (dummy', s) = getLine' dummy
               in (dummy', read s)

main' :: Dummy -> (Dummy, ())
main' dummy0 = let (dummy1, ()) = putStrLn' "What is your name?" dummy0
                   (dummy2, name) = getLine' dummy1
                   (dummy3, ()) = putStrLn' "How old are you?" dummy2
                   (dummy4, age) = getInt dummy3
               in putStrLn' (name ++ ", " ++ show age) dummy4

main = main' (Dummy 0) `seq` pure ()
```

`Dummy -> (Dummy, a)` ã¨ã„ã†å½¢ã®é–¢æ•°ãŒæ²¢å±±å‡ºã¦ãã¾ã—ãŸã­ã€‚ã“ã‚Œã‚’å‹ã‚¨ã‚¤ãƒªã‚¢ã‚¹ `Action a` ã¨ã—ã¦å®šç¾©ã—ã¾ã—ã‚‡ã†ã€‚

```haskell
-- Main3d.hs
import System.IO.Unsafe (unsafePerformIO)

newtype Dummy = Dummy Int
type Action a = Dummy -> (Dummy, a)

{-# OPAQUE putStrLn' #-}
putStrLn' :: String -> Action ()
putStrLn' x dummy = dummy `seq` unsafePerformIO (putStrLn x >> pure (dummy, ()))

{-# OPAQUE getLine' #-}
getLine' :: Action String
getLine' dummy = dummy `seq` unsafePerformIO (getLine >>= \s -> pure (dummy, s))

getInt :: Action Int
getInt dummy = let (dummy', s) = getLine' dummy
               in (dummy', read s)

main' :: Action ()
main' dummy0 = let (dummy1, ()) = putStrLn' "What is your name?" dummy0
                   (dummy2, name) = getLine' dummy1
                   (dummy3, ()) = putStrLn' "How old are you?" dummy2
                   (dummy4, age) = getInt dummy3
               in putStrLn' (name ++ ", " ++ show age) dummy4

main = main' (Dummy 0) `seq` pure ()
```

å‹ã¯ã™ã£ãã‚Šã—ã¾ã—ãŸãŒã€ãƒ€ãƒŸãƒ¼å¤‰æ•°ã‚’ã„ã¡ã„ã¡å—ã‘æ¸¡ã—ã™ã‚‹ã®ã¯é¢å€’ã§ã™ã­ã€‚ãƒ©ãƒƒãƒ—ã—ãŸé–¢æ•°ã‚’ä½œã‚Šã¾ã—ã‚‡ã†ã€‚

ã¾ãšã€é€æ¬¡å®Ÿè¡Œã™ã‚‹é–¢æ•°ã¯æ¬¡ã®ã‚ˆã†ã«æ›¸ã‘ã¾ã™ï¼š

```haskell
andThen :: Action a -> (a -> Action b) -> Action b
andThen f g = \dummy -> case f dummy of
                          (dummy', x) -> g x dummy'
```

æ¬¡ã«ã€å€¤ `x :: a` ã‚’ `Action a` ã«åŸ‹ã‚è¾¼ã‚€é–¢æ•°ã¯æ¬¡ã®ã‚ˆã†ã«æ›¸ã‘ã¾ã™ï¼š

```haskell
value :: a -> Action a
value x = \dummy -> (dummy, x)
```

ã“ã‚Œã‚‰ã‚’ä½¿ã†ã¨ã€å…ˆã»ã©ã®ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã¯æ¬¡ã®ã‚ˆã†ã«æ›¸ã‘ã¾ã™ï¼š

```haskell
-- Main3e.hs
import System.IO.Unsafe (unsafePerformIO)

newtype Dummy = Dummy Int
type Action a = Dummy -> (Dummy, a)

andThen :: Action a -> (a -> Action b) -> Action b
andThen f g = \dummy -> case f dummy of
                          (dummy', x) -> g x dummy'

value :: a -> Action a
value x = \dummy -> (dummy, x)

{-# OPAQUE putStrLn' #-}
putStrLn' :: String -> Action ()
putStrLn' x dummy = dummy `seq` unsafePerformIO (putStrLn x >> pure (dummy, ()))

{-# OPAQUE getLine' #-}
getLine' :: Action String
getLine' dummy = dummy `seq` unsafePerformIO (getLine >>= \s -> pure (dummy, s))

main = main' (Dummy 0) `seq` pure ()

-- ã“ã“ã¾ã§æº–å‚™
-- ã“ã“ã‹ã‚‰æœ¬å½“ã«æ›¸ããŸã„ã‚³ãƒ¼ãƒ‰

getInt :: Action Int
getInt = getLine' `andThen` \s -> value (read s)

main' :: Action ()
main' = putStrLn' "What is your name?" `andThen` \() ->
        getLine' `andThen` \name ->
        putStrLn' "How old are you?" `andThen` \() ->
        getInt `andThen` \age ->
        putStrLn' (name ++ ", " ++ show age)
```

å¾ŒåŠã®ã€Œæœ¬å½“ã«æ›¸ããŸã„ã‚³ãƒ¼ãƒ‰ã€ã®éƒ¨åˆ†ã‹ã‚‰ã¯ãƒ€ãƒŸãƒ¼å¤‰æ•°ãŒãªããªã£ã¦ã™ã£ãã‚Šã—ã¾ã—ãŸã€‚ã„ã„æ„Ÿã˜ã§ã™ã­ã€‚

## çµè«–

ç´”ç²‹é–¢æ•°å‹è¨€èªã§å…¥å‡ºåŠ› (IO) ã‚’æœŸå¾…ã—ãŸé€šã‚Šã«è¡Œã†ã«ã¯ã€ãƒ€ãƒŸãƒ¼ã®å€¤ã‚’å—ã‘æ¸¡ã— `Dummy -> (Dummy, a)` ã™ã‚‹ã®ãŒè‰¯ã„ã“ã¨ãŒã‚ã‹ã‚Šã¾ã—ãŸã€‚ãã—ã¦ã€`type Action a = Dummy -> (Dummy, a)` ã¨å®šç¾©ã—ã¦ `andThen :: Action a -> (a -> Action b) -> Action b` ã¨ `value :: a -> Action a` ã¨ã„ã†2ã¤ã®é–¢æ•°ã‚’ä½¿ã†ã¨ã€ãƒ€ãƒŸãƒ¼ã®å¤‰æ•°ã®å—ã‘æ¸¡ã—ã‚’è¡¨é¢ä¸Šè¦‹ãˆãªãã™ã‚‹ã“ã¨ãŒã§ãã¾ã—ãŸã€‚

å®Ÿã¯ã€Haskellã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ãƒ¼ã§ã‚ã‚‹GHCã«ã¯ã€ã“ã†ã„ã†ã€Œãƒ€ãƒŸãƒ¼ã®å€¤ã€ã‚’è¡¨ã™å‹ã€ãã—ã¦ãã‚Œã‚’å—ã‘æ¸¡ã—ã™ã‚‹é–¢æ•°ã®å‹ `Action` ã«ç›¸å½“ã™ã‚‹ã‚‚ã®ãŒã™ã§ã«ç”¨æ„ã•ã‚Œã¦ã„ã¾ã™ã€‚

[GHC.IO](https://hackage.haskell.org/package/base-4.18.1.0/docs/GHC-IO.html)ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’è¦‹ã¦ã¿ã¾ã—ã‚‡ã†ï¼š

```haskell
module GHC.IO where

newtype IO a = IO (State# RealWorld -> (# State# RealWorld, a #))
```

æˆ‘ã€…ã® `Dummy` å‹ã«ç›¸å½“ã™ã‚‹ã‚‚ã®ã¯GHCã§ã¯ `State# RealWorld` ã¨å‘¼ã°ã‚Œã¦ã„ã¾ã™ã€‚ãã—ã¦ã€`Action` ã«ç›¸å½“ã™ã‚‹ã‚‚ã®ã¯GHCã§ã¯ `IO` ã¨å‘¼ã°ã‚Œã¦ã„ã¾ã™ï¼ˆ`->` ã®å³å´ã® `(# ..., ... #)` ã¯ã‚¿ãƒ—ãƒ«ã®ä¸€ç¨®ã§ã™ï¼‰ã€‚

æˆ‘ã€…ã® `andThen` ã¯GHCã§ã¯ `(>>=)` ã¨å‘¼ã°ã‚Œã€æˆ‘ã€…ã® `value` ã¯GHCã§ã¯ `pure` ã¾ãŸã¯ `return` ã¨å‘¼ã°ã‚Œã¦ã„ã¾ã™ã€‚

ãã†ã€æˆ‘ã€…ã¯ã€Œ`IO` ã«ç¸›ã‚‰ã‚Œãšã«è‡ªç”±ã«å…¥å‡ºåŠ›ã‚’è¡Œã„ãŸã„ã€ã¨å¤¢è¦‹ã¦æ—…ã«å‡ºã¾ã—ãŸãŒã€ç´”ç²‹ãªè¨€èªã§ã‚ã‚‹Haskellã§å®‰å¿ƒã—ã¦å…¥å‡ºåŠ›ã‚’è¡Œã†ã«ã¯ã€çµå±€ `IO` ãŒæœ€é©ã ã£ãŸã®ã§ã™ã€‚

ã—ã‹ã—ã€æ—…ã‚’ã—ãŸã“ã¨ã§ã‚ã‹ã£ãŸã“ã¨ã‚‚ã‚ã‚Šã¾ã™ã€‚GHCã§ã€Œç¾å®Ÿä¸–ç•Œã€ `RealWorld` ã¨å¤§è¢ˆè£Ÿãªåå‰ã§å‘¼ã°ã‚Œã¦ã„ã‚‹ã‚‚ã®ã¯ã€ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ãƒ¼ã®æœ€é©åŒ–ã‚’åˆ¶å¾¡ã™ã‚‹ãŸã‚ã®å˜ãªã‚‹ãƒ€ãƒŸãƒ¼ã®å¼•æ•°ã«éããªã‹ã£ãŸã®ã§ã™ã€‚è¬ãŒä¸€ã¤è§£ã‘ã¾ã—ãŸã‹ï¼Ÿ

ãªãŠã€`State# RealWorld` ã‚’ä½¿ã£ãŸ `IO` å‹ã®å®šç¾©ã¯GHCã®ã‚‚ã®ã§ã‚ã‚Šã€ä»–ã®ï¼ˆéå»ã«å­˜åœ¨ã—ãŸï¼‰Haskellã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ãƒ¼ã§ã¯åˆ¥ã®å†…éƒ¨è¡¨ç¾ãŒä½¿ã‚ã‚Œã¦ã„ã‚‹å¯èƒ½æ€§ãŒã‚ã‚Šã¾ã™ã€‚

## ãŠã¾ã‘ï¼šHaskellãŒã€Œç´”ç²‹ã€ã¨ã¯ã©ã†ã„ã†ã“ã¨ã‹

Haskellã¯ã€Œç´”ç²‹ã€ã¨è¨€ã‚ã‚Œã¾ã™ãŒã€ãã‚Œã¯ã©ã†ã„ã†ã“ã¨ã§ã—ã‚‡ã†ã‹ã€‚æ•°å­¦çš„ãªé–¢æ•°ãŒã©ã†ã®ã“ã†ã®ã€ã¿ãŸã„ãªèª¬æ˜ã‚‚ã‚ã‚‹ã‹ã¨æ€ã„ã¾ã™ãŒã€ã“ã“ã§ã¯ã‚‚ã£ã¨å…·ä½“çš„ãªæŒ™å‹•ã¨ã—ã¦èª¬æ˜ã—ã¦ã¿ã¾ã—ã‚‡ã†ã€‚

ã™ãªã‚ã¡ã€Haskellã¯ã€Œã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ãƒ¼ã«è¨±ã•ã‚Œã‚‹æœ€é©åŒ–ã®ç¯„å›²ãŒåºƒã„è¨€èªã€ã ã¨ã„ã†ã“ã¨ã§ã™ã€‚ã“ã®è¨˜äº‹ã§è¦‹ãŸã‚ˆã†ã«ã€ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ãƒ¼ã¯ä»»æ„ã®é–¢æ•°å‘¼ã³å‡ºã—ã‚’å«ã‚€å¼ã‚’å…±é€šéƒ¨åˆ†å¼å‰Šé™¤ã—ã¦ã‚‚è‰¯ã„ã—ã€è©•ä¾¡ã®é †ç•ªã‚’å…¥ã‚Œæ›¿ãˆã¦ã‚‚è‰¯ã„ã®ã§ã™ã€‚

ã“ã†ã„ã†ã“ã¨ãŒè¨±ã•ã‚Œã‚‹è¨€èªã¯å¤šãã¯ã‚ã‚Šã¾ã›ã‚“ã€‚ã ã‹ã‚‰ã“ãHaskellã¯ã‚ã–ã‚ã–ç´”ç²‹ã¨å‘¼ã°ã‚Œã‚‹ã®ã§ã—ã‚‡ã†ã€‚

## ãŠã¾ã‘2ï¼šæœ¬å½“ã¯æ€–ã„ `unsafePerformIO`

`unsafePerformIO` ã¯åå‰ã«unsafeãŒã¤ãé–¢æ•°ã®ä¸­ã§ã‚‚ã‹ãªã‚Šå±é™ºãªæ–¹ã§ã€ã“ã‚Œã‚’ä½¿ã†ã¨å‹ã‚·ã‚¹ãƒ†ãƒ ã‚’è¿‚å›ã™ã‚‹ã“ã¨ãŒã§ãã¾ã™ã€‚

å…·ä½“çš„ã«ã¯ã€ã‚ã‚‹å‹ã‚’åˆ¥ã®å‹ã«ç„¡ç†ã‚„ã‚Šå¤‰æ›ã™ã‚‹é–¢æ•° `cast :: a -> b` ãŒæ›¸ã‘ã¦ã—ã¾ã†ã®ã§ã™ï¼š

```haskell
import System.IO.Unsafe (unsafePerformIO)
import Data.IORef
import Data.Maybe

cast :: a -> b
cast x = unsafePerformIO $ do
  let ref = unsafePerformIO (newIORef Nothing)
  writeIORef ref (Just x)
  fromJust <$> readIORef ref

main :: IO ()
main = print (cast "Hello!" :: Int)
```

ã“ã®ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã‚’å®Ÿè¡Œã™ã‚‹ã¨ã€ãƒ¡ãƒãƒ£ã‚¯ãƒãƒ£ãªå€¤ãŒè¡¨ç¤ºã•ã‚Œã‚‹ã‹ã¨æ€ã„ã¾ã™ã€‚ãƒ—ãƒ­ã‚°ãƒ©ãƒ ã‚’ã‚¯ãƒ©ãƒƒã‚·ãƒ¥ã•ã›ã‚‹ã“ã¨ã‚‚å®¹æ˜“ã§ã—ã‚‡ã†ã€‚`unsafePerformIO` ã¯å±é™ºãªã®ã§ã™ã€‚
